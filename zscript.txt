version "4.11"

class PerksPowers : EventHandler {
	int powersSpawned;
	
	static const class<PNP_Power> powers[] = {
		"PNP_PowerInstakill",
		"PNP_PowerMaxAmmo",
		"PNP_PowerNuke",
		"PNP_PowerDeathMachine",
		"PNP_PowerFireSale"
	};
	
	override void WorldThingSpawned(WorldEvent e) {
		let mo = Inventory(e.thing);
		
		if (mo && !(mo is "PNP_Power") && mo.pos.xy != (0, 0) && (mo.bBigPowerup || (mo is "Health" && mo.amount >= 100) || (mo is "CustomInventory" && mo.bCountItem)) && !mo.bDropped) {
			for (int i = 0; i < 3; ++i) {
				Vector2 offs = mo.AngleToVector(FRandom(0, 360), mo.radius + FRandom(0, GetDefaultByType("PNP_PowerPerk").radius));
				Actor perk = mo.Spawn("PNP_PowerPerk", mo.pos + (offs.x, offs.y, 0));
				
				if (perk) {
					if (perk.CheckPosition(perk.pos.xy)) {
						break;
					}
					
					perk.Destroy();
				}
			}
		}
	}
	
	override void WorldThingDied(WorldEvent e) {
		let mo = e.thing;
		
		if (mo.target is "PlayerPawn" && Random(0, 100) < sv_pnp_powerChance && powersSpawned < sv_pnp_maxPowers) {
			mo.A_DropItem(powers[Random(0, powers.Size() - 1)]);
			++powersSpawned;
		}
	}
	
	override void PlayerEntered(PlayerEvent e) {
		let mo = players[e.playerNumber].mo;
		
		if (mo) {
			if (!(sv_pnp_perkStay || e.isReturn)) {
				let item = mo.FindInventory("PNP_Perk", true);
				
				while (item) {
					item.Destroy();
					item = mo.FindInventory("PNP_Perk", true);
				}
			}
			
			mo.A_TakeInventory("PNP_ITouchedPerkACola");
		}
	}
}

#include "zscript/perks/base.txt"
#include "zscript/perks/doubletap.txt"
#include "zscript/perks/juggernog.txt"
#include "zscript/perks/phdflopper.txt"
#include "zscript/perks/quickrevive.txt"
#include "zscript/perks/speedcola.txt"
#include "zscript/perks/staminup.txt"
#include "zscript/perks/widowswine.txt"

#include "zscript/powerups/base.txt"
#include "zscript/powerups/deathmachine.txt"
#include "zscript/powerups/firesale.txt"
#include "zscript/powerups/instakill.txt"
#include "zscript/powerups/maxammo.txt"
#include "zscript/powerups/nuke.txt"
#include "zscript/powerups/perk.txt"

#include "zscript/effects.txt"
#include "zscript/particles.txt"